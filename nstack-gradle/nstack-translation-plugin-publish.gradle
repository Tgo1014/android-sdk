apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'signing'

// Username & password for Sonatype, stored in gradle.properties
def _ossrhUsername = System.getenv('NEXUS_USERNAME')
def _ossrhPassword = System.getenv('NEXUS_PASSWORD')

final def _isReleaseVersion = !VERSION_NAME.endsWith("SNAPSHOT")

nexusStaging {
    packageGroup = "dk.nodes"
    delayBetweenRetriesInMillis = 4000
    stagingProfileId = "5a414403b5bf75"
}

jar {
    manifest {
        attributes 'Implementation-Title': POM_NAME, 'Implementation-Version': VERSION_NAME
    }
}

afterEvaluate {

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set('javadoc')
        from javadoc
    }

    publishing {
        publications {
            nstackTranslationPluginJar(MavenPublication) {

                artifact sourcesJar
                artifact javadocJar
                from components.java

                pom {
                    name = POM_NAME
                    packaging = POM_PACKAGING
                    artifactId = POM_ARTIFACT_ID
                    description = POM_DESCRIPTION

                    groupId = GROUP
                    version = VERSION_NAME
                    url = POM_URL

                    inceptionYear = '2015'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'johsoe'
                            name = 'Johnny SÃ¸rensen'
                            email = 'joso@nodes.dk'
                        }
                    }

                    scm {
                        url = 'https://github.com/nodes-android/nstack-gradle'
                        connection = 'scm:https://github.com/nodes-android/nstack-gradle'
                    }

                    issueManagement {
                        system = 'GitHub issues'
                        url = 'https://github.com/nodes-android/nstack-gradle/issues'
                    }
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username _ossrhUsername
                    password _ossrhPassword
                }

                final def releasesRepoUrl = 'https://oss.sonatype.org/content/repositories/releases'
                final def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'

                url = _isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            }
        }

        signing {
            required {
                return _isReleaseVersion
            }

            sign publishing.publications.nstackTranslationPluginJar
        }
    }
}