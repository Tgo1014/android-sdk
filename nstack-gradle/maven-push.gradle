apply plugin: 'signing'
apply plugin: 'maven'

// Username & password for Sonatype, stored in gradle.properties
def _ossrhUsername = System.getenv('NEXUS_USERNAME')
def _ossrhPassword = System.getenv('NEXUS_PASSWORD')

allprojects { ext."signing.keyId" = System.getenv('keyId') }
allprojects { ext."signing.secretKeyRingFile" = System.getenv('secretKeyRingFile') }
allprojects { ext."signing.password" = System.getenv('password') }

// Artifact settings

nexusStaging {
    packageGroup = "dk.nodes"
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

jar {
    manifest {
        attributes 'Implementation-Title': POM_NAME, 'Implementation-Version': VERSION_NAME
    }
}

task androidJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, androidJavadocsJar
}

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            pom.groupId = GROUP
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = VERSION_NAME

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: _ossrhUsername, password: _ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: _ossrhUsername, password: _ossrhPassword)
            }

            pom.project {
                name POM_NAME
                packaging POM_PACKAGING
                artifactId POM_ARTIFACT_ID
                description POM_DESCRIPTION
                url 'https://github.com/nodes-android/nstack-gradle'
                inceptionYear '2015'

                scm {
                    url POM_SCM_URL
                    connection POM_SCM_CONNECTION
                }

                licenses {
                    license {
                        name POM_LICENCE_NAME
                        url POM_LICENCE_URL
                    }
                }

                developers {
                    developer {
                        id POM_DEVELOPER_ID
                        name POM_DEVELOPER_NAME
                        email POM_DEVELOPER_EMAIL
                    }
                }

                issueManagement {
                    system 'GitHub issues'
                    url 'https://github.com/nodes-android/nstack-gradle/issues'
                }
            }
        }
    }
}
