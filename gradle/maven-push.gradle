apply plugin: 'signing'
apply plugin: 'maven-publish'

// Username & password for Sonatype, stored in gradle.properties
def _ossrhUsername = System.getenv('NEXUS_USERNAME')
def _ossrhPassword = System.getenv('NEXUS_PASSWORD')

allprojects { ext."signing.keyId" = System.getenv('keyId') }
allprojects { ext."signing.secretKeyRingFile" = System.getenv('secretKeyRingFile') }
allprojects { ext."signing.password" = System.getenv('password') }

// Artifact settings
final def _isReleaseVersion = !VERSION_NAME.endsWith("SNAPSHOT")

nexusStaging {
    packageGroup = "dk.nodes"
    delayBetweenRetriesInMillis = 4000
    stagingProfileId = "5a414403b5bf75"
}

afterEvaluate {



    publishing {
        publications {
            nstackKotlin(MavenPublication) {
                pom {
                    name = POM_NAME
                    packaging = POM_PACKAGING
                    artifactId = POM_ARTIFACT_ID
                    groupId = GROUP
                    description = POM_DESCRIPTION
                    version = VERSION_NAME
                    url = POM_URL
                    inceptionYear = '2015'

                    /*artifact project.sourcesJar {
                        classifier "sources"
                    }*/



                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                        }
                    }

                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_EMAIL
                        }
                    }

                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                    }

                    issueManagement {
                        system = 'GitHub issues'
                        url = 'https://github.com/nodes-android/nstack-kotlin/issues'
                    }
                }
            }
        }

        repositories {
            maven {

                credentials {
                    username _ossrhUsername
                    password _ossrhPassword
                }

                final def releasesRepoUrl = 'https://oss.sonatype.org/content/repositories/releases'
                final def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'

                url = _isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            }
        }
    }


    def plugins = project.getPlugins()
    if (plugins.hasPlugin('com.android.application') || plugins.hasPlugin('com.android.library')) {
        task androidSourcesJar(type: Jar) {
            classifier = 'sources'
            from android.sourceSets.main.java.sourceFiles
        }

        artifacts {
            archives androidSourcesJar
        }
    }
    else {
        task('sourcesJar', type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }
        task javadocsJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        artifacts {
            archives sourcesJar
            archives javadocsJar
        }
    }

    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }




}



signing {
    required { gradle.taskGraph.hasTask("publishNstackKotlinPublicationToMavenRepository") }
    sign configurations.archives
}


