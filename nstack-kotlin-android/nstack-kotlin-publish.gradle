apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'digital.wup.android-maven-publish'

// Username & password for Sonatype, stored in gradle.properties
def _ossrhUsername = System.getenv('NEXUS_USERNAME')
def _ossrhPassword = System.getenv('NEXUS_PASSWORD')

allprojects { ext."signing.keyId" = System.getenv('keyId') }
allprojects { ext."signing.secretKeyRingFile" = System.getenv('secretKeyRingFile') }
allprojects { ext."signing.password" = System.getenv('password') }

// Artifact settings
final def _isReleaseVersion = !VERSION_NAME.endsWith("SNAPSHOT")

nexusStaging {
    packageGroup = "dk.nodes"
    delayBetweenRetriesInMillis = 4000
    stagingProfileId = "5a414403b5bf75"
}

afterEvaluate {
    publishing {
        publications {
            nstackKotlinAar(MavenPublication) {

                // Tell maven to prepare the generated "*.aar" file for publishing
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar") {
                    builtBy assembleRelease
                }

                pom {
                    artifactId = POM_ARTIFACT_ID
                    name = POM_NAME
                    packaging = POM_PACKAGING
                    groupId = GROUP
                    description = POM_DESCRIPTION
                    version = VERSION_NAME
                    url = POM_URL
                    inceptionYear = '2015'

                    from components.android

                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                        }
                    }

                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_EMAIL
                        }
                    }

                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                    }

                    issueManagement {
                        system = 'GitHub issues'
                        url = 'https://github.com/nodes-android/nstack-kotlin/issues'
                    }
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username _ossrhUsername
                    password _ossrhPassword
                }

                final def releasesRepoUrl = 'https://oss.sonatype.org/content/repositories/releases'
                final def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'

                url = _isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            }
        }

        if (JavaVersion.current().isJava8Compatible()) {
            allprojects {
                tasks.withType(Javadoc) {
                    options.addStringOption('Xdoclint:none', '-quiet')
                }
            }
        }
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set("javadoc")

        from javadoc.destinationDir
    }

    signing {
        required { gradle.taskGraph.hasTask("publishNstackKotlinPublicationToMavenRepository") }
        sign configurations.archives
    }
}
